# Forge Standard Library ‚Ä¢ [![CI status](https://github.com/foundry-rs/forge-std/actions/workflows/ci.yml/badge.svg)](https://github.com/foundry-rs/forge-std/actions/workflows/ci.yml)

Forge Standard Library est une collection de contrats et biblioth√®ques utiles pour √™tre utilis√©s avec [Forge and Foundry](https://github.com/foundry-rs/foundry).Elle tire parti des cheatcodes de Forge pour rendre l'√©criture des tests plus simple et plus rapide, tout en am√©liorant l'exp√©rience utilisateur des cheatcodes.

**Apprenez √† utiliser Forge-Std avec le  [üìñ Livre Foundry (Guide Forge-Std))](https://book.getfoundry.sh/forge/forge-std.html).**

## Installation

```bash
forge install foundry-rs/forge-std
```

## Contrats
### stdError

Ceci est un contrat utilitaire pour les erreurs et les revert. Dans Forge, ce contrat est particuli√®rement utile pour le cheatcode `expectRevert`, car il fournit toutes les erreurs int√©gr√©es au compilateur.

Consultez le contrat lui-m√™me pour obtenir tous les codes d'erreur.

#### Exemple d'utilisation

```solidity

import "forge-std/Test.sol";

contract TestContract is Test {
    ErrorsTest test;

    function setUp() public {
        test = new ErrorsTest();
    }

    function testExpectArithmetic() public {
        vm.expectRevert(stdError.arithmeticError);
        test.arithmeticError(10);
    }
}

contract ErrorsTest {
    function arithmeticError(uint256 a) public {
        uint256 a = a - 100;
    }
}
```

### stdStorage

C'est un contrat assez volumineux en raison de la surcharge n√©cessaire pour rendre l'exp√©rience utilisateur agr√©able. Principalement, il s'agit d'un wrapper autour des cheatcodes `record` et `accesses`. Il peut *toujours* trouver et √©crire dans les slot(s) de stockage associ√©s √† une variable particuli√®re sans conna√Ætre la disposition du stockage. La seule _grosse_ mise en garde est que, bien qu'un slot puisse √™tre trouv√© pour des variables de stockage pack√©es, nous ne pouvons pas √©crire en toute s√©curit√© dans cette variable. Si un utilisateur tente d'√©crire dans un slot pack√©, l'ex√©cution renvoie une erreur, sauf s'il est non initialis√© (`bytes32(0)`).

Cela fonctionne en enregistrant tous les `SLOAD` et `SSTORE` pendant un appel de fonction. S'il y a un seul slot lu ou √©crit, il renvoie imm√©diatement le slot. Sinon, en coulisses, nous it√©rons et v√©rifions chacun d'eux (√† condition que l'utilisateur ait pass√© un param√®tre `depth`). Si la variable est une struct, vous pouvez passer un param√®tre `depth` qui correspond essentiellement √† la profondeur du champ.

I.e.:
```solidity
struct T {
    // depth 0
    uint256 a;
    // depth 1
    uint256 b;
}
```

#### Exemple d'utilisation:

```solidity
import "forge-std/Test.sol";

contract TestContract is Test {
    using stdStorage for StdStorage;

    Storage test;

    function setUp() public {
        test = new Storage();
    }

    function testFindExists() public {
        // Disons que nous voulons trouver le slot pour la variable publique `exists`. 
        // Il suffit de passer le s√©lecteur de fonction √† la commande `find`.
        uint256 slot = stdstore.target(address(test)).sig("exists()").find();
        assertEq(slot, 0);
    }

    function testWriteExists() public {
        // Disons que nous voulons √©crire dans le slot pour la variable publique `exists`.
        // Il suffit de passer le s√©lecteur de fonction √† la commande `checked_write`.
        stdstore.target(address(test)).sig("exists()").checked_write(100);
        assertEq(test.exists(), 100);
    }

    // Il prend en charge des dispositions de stockage arbitraires, comme les emplacements de stockage bas√©s sur assembly
    function testFindHidden() public {
        // `hidden` est un hachage al√©atoire de bytes, l'it√©ration √† travers les slots ne le trouverait pas. 
        // Notre m√©canisme le trouve.
        // Vous pouvez √©galement utiliser le s√©lecteur au lieu d'une cha√Æne de caract√®res.
        uint256 slot = stdstore.target(address(test)).sig(test.hidden.selector).find();
        assertEq(slot, uint256(keccak256("my.random.var")));
    }

    // Si vous ciblez un mapping, vous devez passer les cl√©s n√©cessaires pour effectuer la recherche
    // i.e.:
    function testFindMapping() public {
        uint256 slot = stdstore
            .target(address(test))
            .sig(test.map_addr.selector)
            .with_key(address(this))
            .find();
        // dans le constructeur de `Storage`, nous avons √©crit que la valeur de cette adresse √©tait 1 dans la map
        // donc lorsque nous chargeons le slot, nous nous attendons √† ce qu'il soit √† 1
        assertEq(uint(vm.load(address(test), bytes32(slot))), 1);
    }

    // Si la cible est une struct, vous pouvez sp√©cifier la profondeur du champ :
    function testFindStruct() public {
        // REMARQUE : voir le param√®tre de profondeur - 0 signifie le 0√®me champ, 1 signifie le 1er champ, etc.
        uint256 slot_for_a_field = stdstore
            .target(address(test))
            .sig(test.basicStruct.selector)
            .depth(0)
            .find();

        uint256 slot_for_b_field = stdstore
            .target(address(test))
            .sig(test.basicStruct.selector)
            .depth(1)
            .find();

        assertEq(uint(vm.load(address(test), bytes32(slot_for_a_field))), 1);
        assertEq(uint(vm.load(address(test), bytes32(slot_for_b_field))), 2);
    }
}

// Un contrat de stockage complexe
contract Storage {
    struct UnpackedStruct {
        uint256 a;
        uint256 b;
    }

    constructor() {
        map_addr[msg.sender] = 1;
    }

    uint256 public exists = 1;
    mapping(address => uint256) public map_addr;
    // mapping(address => Packed) public map_packed;
    mapping(address => UnpackedStruct) public map_struct;
    mapping(address => mapping(address => uint256)) public deep_map;
    mapping(address => mapping(address => UnpackedStruct)) public deep_map_struct;
    UnpackedStruct public basicStruct = UnpackedStruct({
        a: 1,
        b: 2
    });

    function hidden() public view returns (bytes32 t) {
        // un slot de stockage extr√™mement cach√©
        bytes32 slot = keccak256("my.random.var");
        assembly {
            t := sload(slot)
        }
    }
}

```

### stdCheats

Il s'agit d'un wrapper autour de cheatcodes divers qui n√©cessitent des wrappers pour √™tre plus conviviaux pour les d√©veloppeurs. Actuellement, il n'y a que des fonctions li√©es √†  `prank`.En g√©n√©ral, les utilisateurs peuvent s'attendre √† ce que de l'ETH soit ajout√© √† une adresse lors de l'utilisation de `prank`,  mais ce n'est pas le cas pour des raisons de s√©curit√©. La fonction `hoax` doit uniquement √™tre utilis√©e pour une adresse avec un solde attendu, car elle sera √©cras√©e. Si une adresse poss√®de d√©j√† de l'ETH, il suffit d'utiliser `prank`.  Si vous souhaitez modifier explicitement ce solde, utilisez simplement `deal`. Si vous voulez faire les deux, `hoax` est √©galement adapt√©.


#### Exemple d'utilisation:

```solidity

// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "forge-std/Test.sol";

// H√©rite de stdCheats
contract StdCheatsTest is Test {
    Bar test;
    function setUp() public {
        test = new Bar();
    }

    function testHoax() public {
        // nous appelons `hoax`, qui donne √† l'adresse cible de l'eth
        // puis appelle `prank`
        hoax(address(1337));
        test.bar{value: 100}(address(1337));

        // surcharg√© pour vous permettre de sp√©cifier combien d'eth
        // initialiser sur l'adresse
        hoax(address(1337), 1);
        test.bar{value: 1}(address(1337));
    }

    function testStartHoax() public {
        // nous appelons `startHoax`, qui donne √† l'adresse cible de l'eth
        // puis appelle `startPrank`
        //
        // il est √©galement surcharg√© pour que vous puissiez sp√©cifier un montant d'eth
        startHoax(address(1337));
        test.bar{value: 100}(address(1337));
        test.bar{value: 100}(address(1337));
        vm.stopPrank();
        test.bar(address(this));
    }
}

contract Bar {
    function bar(address expectedSender) public payable {
        require(msg.sender == expectedSender, "!prank");
    }
}
```

### Std Assertions

Contient diverses assertions.

### `console.log`

L'utilisation suit le m√™me format que [Hardhat](https://hardhat.org/hardhat-network/reference/#console-log).
Il est recommand√© d'utiliser `console2.sol` comme montr√© ci-dessous, car cela affichera les logs d√©cod√©s dans les traces Forge.

```solidity
// l'importer indirectement via Test.sol
import "forge-std/Test.sol";
// ou l'importer directement
import "forge-std/console2.sol";
...
console2.log(someValue);
```

Si vous avez besoin de compatibilit√© avec Hardhat, vous devez utiliser `console.sol`  standard √† la place. En raison d'un bug dans `console.sol`,  les logs qui utilisent les types `uint256` ou `int256` ne seront pas correctement d√©cod√©s dans les traces Forge.

```solidity
// l'importer indirectement via Test.sol
import "forge-std/Test.sol";
// ou l'importer directement
import "forge-std/console.sol";
...
console.log(someValue);
```

## License

Forge Standard Library est propos√© sous licence [MIT](LICENSE-MIT) or [Apache 2.0](LICENSE-APACHE) license.
